// Class serialization/deserialization using supplied functions
import crack.io Formatter, Writer, cout, FDReader, FDWriter, FStr;
import crack.lang ManagedBuffer, Buffer, InvalidArgumentError,
                  InvalidResourceError,  AssertionError;
import crack.runtime INT_SIZE;
import crack.strutil StringArray;
import crack.ascii hex, escape, bin;
import crack.cont.array Array;
import crack.cont.hashmap HashMap, OrderedHashMap;
import crack.enc.json.parser JSONParser;
import crack.enc.json.lib UnexpectedToken, ParseException,
                      JSONFormatter, JSONStringFormatter, JSONObject,
                      JSONArray, JSONInt, JSONFloat, JSONBool, JSONString;
import crack.db.mongo_support OID;

import whip.serializer Materializer, Serializer, Serializable;
@import crack.ann define;

StringArray basicTypes = ["int", "String", "float", "bool", "object",
                          "array[int]", "array[string]", "array[float]",
                          "array[bool]", "array[object]"
                         ];

StringArray crackTypes = ["int32", "uint32", "int64", "uint64",
                          "String", "float32", "float64", 
                          "Array[int]", "Array[uint]", "Array[int32]",
                          "Array[uint32]", "Array[int64]", "Array[uint64]",
                          "Array[float32]", "Array[float64]",
                         ];

StringArray crackSizes = ["4", "4", "8", "8", ".size", "4", "8",
                          ".count()", ".count()", ".count()", 
                          ".count()", ".count()", ".count()",
                          ".count()", ".count()"];
                          

uint id = 0;

class noCopyStringFormatter : JSONStringFormatter {
    oper init(): JSONStringFormatter() {}
    oper init() : JSONStringFormatter() {}
    Buffer getBuffer() { return Buffer(_writer.buffer, _writer.size); }
}

// Serializer for JSON formatter buffers
class JSONBufferSerializer : Serializer {

    noCopyStringFormatter _jsonBuf;
    //ManagedBuffer _jsonBuf;
    Array[bool] _sepStack = {};
    uint __id;

    void _setup() {
        writeId = false;
        hasPrefix = true;
        hasPostfix = true;
        itemSep = ", ";
        __id = id++;
    }

    oper init () : _jsonBuf() {
        _setup();
    }

    oper init (uint bufsize) {
        _setup();
    }

    oper init(noCopyStringFormatter fmt) : _jsonBuf = fmt {
        _setup();
    }

    oper init(FDWriter w) : _jsonBuf = null {
        throw InvalidResourceError("Not implemented");
    }

    void prefix(Buffer name) {
        if (!(name is null) && name.size > 0) {
            write(FStr() `$name:`);
        }
        write("{");
    }

    void postfix(Buffer name) {
        if (_sepStack.count() > 0)
            _sepStack.pop();
        write("}");
    }

    Buffer serialized() {
        if (_jsonBuf is null) {
            throw InvalidResourceError("Internal buffer is null");
        }
        return _jsonBuf.getBuffer();
    }

    void _pushSep() {
        _sepStack.append(true);
    }

    void _popSep() {
        if (_sepStack.count() > 0) {
            _sepStack.pop();
            _jsonBuf.write(itemSep);
        }
    }

    void _popSepQuiet() {
        if (_sepStack.count() > 0) {
            _sepStack.pop();
        }
    }

    @define formatMethod(T) {
        void format(T val, Buffer name) {
            _popSep();
            _jsonBuf.format(name);
            _jsonBuf.write(": ");
            _jsonBuf.format(val);
            _pushSep();
        }

        void format(Array[T] val, Buffer name) {
            _popSep();
            _jsonBuf.format(name);
            _jsonBuf.write(": [");

            bool first = true;
            for (elem :in val) {
                if (!first)
                    _jsonBuf.write(", ");
                else
                    first = false;
                _jsonBuf.format(elem);
            }
            _jsonBuf.write("]");
            _pushSep();
        }
    }

    @formatMethod(int)
    @formatMethod(uint)

    @formatMethod(int16)
    @formatMethod(uint16)

    @formatMethod(int32)
    @formatMethod(uint32)

    @formatMethod(int64)
    @formatMethod(uint64)

    @formatMethod(float32)
    @formatMethod(float64)
    @formatMethod(bool)

    @formatMethod(String)

    void format(OID val, Buffer name) {
        _popSep();
        _jsonBuf.format(name);
        _jsonBuf.write(': ');
        if (val is null)
            _jsonBuf.write('null');
        else
            _jsonBuf `$(hex(val))`;
        _pushSep();
    }
    

    Serializer startArray(uint size, Buffer name) {
        _popSep();
        if (name) {
            _jsonBuf.format(name);
            _jsonBuf.write(": ");
        }
        _jsonBuf.write("[");
        _pushSep();
        return JSONBufferSerializer(_jsonBuf);
    }

    bool appendItem(Serializable obj, uint ind) {
        _popSep();
        obj.serialize(JSONBufferSerializer(_jsonBuf), null);
        _pushSep();
        return true;
    }

    bool endArray(Serializer sr, Buffer name) {
        sr.write("]");
        return true;
    }

    void write(Buffer buf) {
        _jsonBuf.write(buf);
    }
}

class JSONBufferMaterializer : Materializer {

    Buffer _jsonBuf;
    JSONParser _parser;
    JSONObject _result;

    void _initResult(Object result) {
        if (result.isa(JSONObject))
            _result = JSONObject.cast(result);
        else
            throw InvalidArgumentError("JSON text must have an object (map) result");
    }

    oper init() {} // Needed for generator

    oper init(Buffer buf) : _jsonBuf = buf, _parser() {
        _initResult(_parser.parse(_jsonBuf).result);
    }

    oper init(Object jsonObject) : _jsonBuf = null {
        _initResult(jsonObject);
    }

    oper init(FDReader r) : _jsonBuf = null {
        throw InvalidResourceError("Not implemented");
    }

    Buffer getBuffer() {
        return _jsonBuf;
    }

    JSONObject getResult() {
        return _result;
    }

    void prefix(Buffer name) {  }

    void postfix(Buffer name) {  }


    @define formatMethodDirect(T, jT) {
        T format(T val, Buffer name) {
            return jT.cast(_result[String(name)]).value;
        }

        Array[T] format(Array[T] val, Buffer name) {
            jA := JSONArray.cast(_result[String(name)]);
            A := Array[T](jA.count());
            for (elem :in jA) {
                A.append(jT.cast(elem).value);
            }
            return A;
        }

        OrderedHashMap[String, T] format(OrderedHashMap[String, T] map, Buffer name) {
            jM := JSONObject.cast(_result);
            M := OrderedHashMap[String, T]();
            for (item :in jM) {
                M[item.key] = jT.cast(item.val).value;
            }
            return M;
        }
    }

    @define formatMethodSimple(T, jT) {
        T format(T val, Buffer name) {
            return T(jT.cast(_result[String(name)]).value);
        }

        Array[T] format(Array[T] val, Buffer name) {
            jA := JSONArray.cast(_result[String(name)]);
            A := Array[T](jA.count());
            for (elem :in jA) {
                A.append(T(jT.cast(elem).value));
            }
            return A;
        }

        OrderedHashMap[String, T] format(OrderedHashMap[String, T] map, Buffer name) {
            jM := JSONObject.cast(_result);
            M := OrderedHashMap[String, T]();
            for (item :in jM) {
                M[item.key] = T(jT.cast(item.val).value);
            }
            return M;
        }
    }

    @define formatMethodComplex(T, jT) {
        T format(T val, Buffer name) {
            return T(jT.cast(_result[String(name)]));
        }

        Array[T] format(Array[T] val, Buffer name) {
            jO := _result[String(name)];
            if (!jO.isa(JSONArray))
                throw InvalidArgumentError(FStr()
                  `Expected JSONArray when materializing $jO`);
            jA := JSONArray.cast(jO);
            A := Array[T](jA.count());

            for (elem :in jA) {
                A.append(T(jT.cast(elem)));
            }
            return A;
        }

        OrderedHashMap[String, T] format(OrderedHashMap[String, T] map, Buffer name) {
            M := OrderedHashMap[String, T]();
            for (item :in _result) {
                M[item.key] = T(jT.cast(item.val));
            }
            return M;
        }
    }

    @formatMethodDirect(int, JSONInt)
    @formatMethodSimple(uint,JSONInt)

    @formatMethodSimple(int16, JSONInt)
    @formatMethodSimple(int32, JSONInt)
    @formatMethodSimple(uint16, JSONInt)
    @formatMethodSimple(uint32, JSONInt)

    @formatMethodSimple(int64, JSONInt)
    @formatMethodSimple(uint64, JSONInt)

    @formatMethodDirect(float, JSONFloat)
    @formatMethodSimple(float32, JSONFloat)
    @formatMethodSimple(float64, JSONFloat)
    @formatMethodDirect(bool, JSONBool)

    @formatMethodComplex(String, JSONString)
    OID format(OID val, Buffer name) {
        jO := _result[String(name)];
        if (jO is null) return OID();
        if (!jO.isa(JSONString))
            throw InvalidArgumentError(FStr()
                  `Expected String when materializing $jO`);
        return OID(bin(Buffer.cast(jO)));
    }


    void write(Buffer buf) { }

    class JSONBufferMaterializerIter : MaterializerIter {
        int __index = -1;
        bool __first = true;;
        JSONArray __A = null;
        

        oper init(Materializer mt, Buffer name) {
            if (!mt.isa(JSONBufferMaterializer))
                throw InvalidArgumentError("JSONBufferMaterializerIter constructor called with incorrect Materializer type");
            jmt := JSONBufferMaterializer.cast(mt);
            if (jmt.getResult().count() > 0) {
                obj := jmt.getResult()[String(name)];
                if (!obj.isa(JSONArray))
                    throw InvalidResourceError("Attempting to iterate through non-Array");
                __A = JSONArray.cast(obj);
            }
        }

        bool nx() {
            if (__first) {
                __first = false;
                return !(__A is null) && __A.count() > 0;
            } else {
                return next();
            }
        }

        bool next() { return ++__index < __A.count();}
        
        Materializer elem() {
            obj := __A[__index];
            if (!obj.isa(JSONObject))
                throw InvalidResourceError("Attempting to access JSONArray item as a JSONObject");
            return JSONBufferMaterializer(JSONObject.cast(obj));
        }
    }

    MaterializerIter iter(Buffer name) {
        return JSONBufferMaterializerIter(this, name);
    }


}
